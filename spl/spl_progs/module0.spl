// resource manager module 
// funtion number = 8 -> aquire terminal 
// funtion number = 9 -> release termial 
// funtion number = 3 -> aquire disk 
alias functionNum R1;
alias currentPID R2;
if (functionNum == 3) then 
    while([DISK_STATUS_TABLE] == 1) do
        [PROCESS_TABLE + currentPID * 16 + 4] = WAIT_DISK;
        multipush(R0, R1, R2, R3);
        call MOD_5;
        multipop(R0, R1, R2, R3);
    endwhile;
    [DISK_STATUS_TABLE] = 1;
    [DISK_STATUS_TABLE + 4] = currentPID ;
    return;
endif;
if (functionNum == 8) then 
    while ([TERMINAL_STATUS_TABLE] == 1) do
        [PROCESS_TABLE + currentPID * 16 + 4] = WAIT_TERMINAL;
        multipush(R0, R1, R2, R3);
        call MOD_5;
        multipop(R0, R1, R2, R3);
    endwhile;
    [TERMINAL_STATUS_TABLE + 1] = currentPID;
    [TERMINAL_STATUS_TABLE] = 1;
    return;
endif ;
if (functionNum == 9) then 
    if ([TERMINAL_STATUS_TABLE + 1] != currentPID) then
        R0 = -1;
        return;
    else
        [TERMINAL_STATUS_TABLE] = 0;
        alias counter R0;
        counter = 1;
        while (counter < 16) do
            if ([PROCESS_TABLE + counter * 16 + 4] == WAIT_TERMINAL) then 
                [PROCESS_TABLE + counter * 16 + 4] = READY;
            endif;
            counter = counter + 1;
        endwhile;
        R0 = 0;
        return;
    endif;
endif ;